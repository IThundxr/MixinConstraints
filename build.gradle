plugins {
    id "com.vanniktech.maven.publish" version "0.28.0"
    id "java"
}

version = "1.0.1"
group = "com.moulberry.mixinconstraints"

repositories {
    maven { url = "https://jitpack.io" }
    maven { url = "https://repo.spongepowered.org/maven/" }
    maven { url = "https://maven.fabricmc.net/" }
    //maven { url = "https://maven.neoforged.net/releases" }
    maven { url = "https://maven.minecraftforge.net" }
    mavenCentral()
}

dependencies {
    // General
    compileOnly "org.spongepowered:mixin:0.8"
    compileOnly "org.ow2.asm:asm-debug-all:5.2"
    compileOnly "io.github.llamalad7:mixinextras-fabric:0.3.5"
    implementation 'org.slf4j:slf4j-api:2.0.9'

    // Platform-specific
    compileOnly "net.fabricmc:fabric-loader:0.14.25" // NO-VER ~ Fabric Loader is mostly version independent
    //compileOnly("net.neoforged:neoforge:20.6.62-beta") // 1.20.6
    compileOnly("net.minecraftforge:forge:1.20.6-50.0.22") // 1.20.6
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

import com.vanniktech.maven.publish.JavaLibrary
import com.vanniktech.maven.publish.JavadocJar
import com.vanniktech.maven.publish.SonatypeHost

mavenPublishing {
    configure(new JavaLibrary(new JavadocJar.Javadoc(), true))

    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)

    signAllPublications()

    coordinates("com.moulberry", "mixinconstraints", version)

    pom {
        name = 'MixinConstraints'
        description = 'Library to enable/disable mixins using annotations'
        url = "https://github.com/Moulberry/MixinConstraints"

        licenses {
            license {
                name = 'MIT License'
                url = 'https://opensource.org/license/mit'
            }
        }
        developers {
            developer {
                name = 'Moulberry'
            }
        }
        scm {
            url = "https://github.com/Moulberry/MixinConstraints/"
            connection = "scm:git:git://github.com/Moulberry/MixinConstraints.git"
            developerConnection = "scm:git:ssh://git@github.com/Moulberry/MixinConstraints.git"
        }
    }
}
